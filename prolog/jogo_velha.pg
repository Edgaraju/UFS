%% Jogo da Velha
%% -----------------------------------------------------------------------------
%% tab(_,_,o, _,_,_, _,_,_) % tabuleiro com um termo
%% tab(1 2 3 4 5 6 7 8 9)

%%
emlinha3([1,2,3]). %% horiz %% posiÂ¸c~oes em linha
emlinha3([4,5,6]).
emlinha3([7,8,9]).
emlinha3([1,4,7]). %% vert
emlinha3([2,5,8]).
emlinha3([3,6,9]).
emlinha3([1,5,9]). %% diag
emlinha3([3,5,7]).

%%
moveC(N,Tab,C):- arg(N,Tab,C).
cruz(N,Tab) :- arg(N,Tab,V), nonvar(V), V=x.
bola(N,Tab) :- arg(N,Tab,V), nonvar(V), V=o.
vazia(N,Tab) :- arg(N,Tab,V), var(V).
cheia(N,Tab) :- \+ vazia(N,Tab).

%%------------------------------------------------------------------------------
gameOver(T,V) :- vence(T,V).
gameOver(T,empate) :- empate(T).
vence(T,venceu(cruz)):- emlinha3([A,B,C]), cruz(A,T),cruz(B,T),cruz(C,T),!.
vence(T,venceu(bola)):- emlinha3([A,B,C]), bola(A,T),bola(B,T),bola(C,T),!.
preenche(XO,T) :-   member(X,[1,2,3,4,5,6,7,8,9]),
                    vazia(X,T),moveC(X,T,XO),!,preenche(XO,T).
preenche(XO,T).

empate(T):- preenche(o,T),\+ vence(T,_),!,preenche(x,T),\+ vence(T,_).
%% ?- (T=tab(o,_,x, _,_,_ ,_,_,_), empate(T)).
%% ?- (T=tab(o,o,x, x,x,o,o,o,x), empate(T)).
%%------------------------------------------------------------------------------

testaOk(P,Tab) :-   vazia(P,Tab),arg(P,Tab,'x'),!;
                    write('Jogada invalida,tente outra!'),nl,
                    escolheMov(Tab,oponente).
escolheMov(T, oponente):-   write('jogue (1..9):'),nl,
                            read(P), testaOk(P,T).
escolheMov(T, computador):-
                ameaca(T,bola,W),!,moveC(W,T,o),! %% vence
                ; ameaca(T,cruz,W),!,moveC(W,T,o),! %% defesa
                ; vazia(5,T),moveC(5,T,'o'),!
                ; chute(9,W),member(W,[1,3,7,9]),vazia(W,T),moveC(W,T,'o'),!
                ; chute(9,W),member(W,[2,4,6,8]),vazia(W,T),moveC(W,T,'o'),!.

%%
ameaca(Tab,CB,W) :- emlinha3(Pos),ameaca(CB,Pos,Tab,W),!.
%%
ameaca(cruz,[A,B,C],T,A) :- vazia(A,T),cruz(B,T),cruz(C,T).
ameaca(cruz,[A,B,C],T,B) :- vazia(B,T),cruz(A,T),cruz(C,T).
ameaca(cruz,[A,B,C],T,C) :- vazia(C,T),cruz(A,T),cruz(B,T).
%%
ameaca(bola,[A,B,C],T,A) :- vazia(A,T),bola(B,T),bola(C,T).
ameaca(bola,[A,B,C],T,B) :- vazia(B,T),bola(A,T),bola(C,T).
ameaca(bola,[A,B,C],T,C) :- vazia(C,T),bola(A,T),bola(B,T).

%%------------------------------------------------------------------------------
%% desenha o tabuleiro
wrtLinha(X,Y,Z,T):-     arg(X,T,V1), wVal(V1),write('|'),
                        arg(Y,T,V2), wVal(V2),write('|'),
                        arg(Z,T,V3), wVal(V3),nl.
wVal(X):- var(X)->write(' ');write(X).
desenha(T) :-   nl, tab(7),wrtLinha(1,2,3,T), tab(7),write('------'),nl,
                tab(7),wrtLinha(4,5,6,T), tab(7),write('------'),nl,
                tab(7),wrtLinha(7,8,9,T).
%% ?- T=tab(_,_,o, _,x,_, _,_,_), desenha(T).

%%------------------------------------------------------------------------------
%% esquema principal do jogo
jogo :-     T = tab(A,B,C, D,E,F, G,H,I),
            exibeJogo(T, inicio),
            jogar(T, oponente).
jogar(T, Jogador):- gameOver(T,Result),!,msgFim(Result).
jogar(T, Jogador):- escolheMov(T, Jogador),!,
                    exibeJogo(T, Jogador),!,
                    proximo(Jogador, Oponente), !,
                    jogar(T, Oponente).
proximo(computador,oponente).
proximo(oponente,computador).
%%
exibeJogo(T,J):- write('jogou:'),write(J),desenha(T).
msgFim(X):-write('GAME OVER:'), write(X),nl,nl.
%%
%% chute(N,S):- repeat, S is random(N).
chute(N,S):- repeat, random(0,N,S).


%-------------------------------------------------------------------------------
%% base para teste
t0(tab(o,o,o, *,*,*, *,*,*)).
t1(tab(o,x,o, x,o,x, o,x,o)).
t2(tab(o,x,o, x,*,x, *,*,*)).
%% ?- t0(T), vence(T,V).
%% ?- t1(T), gameOver(T,O).
%% ?- t2(T), gameOver(T,O).
